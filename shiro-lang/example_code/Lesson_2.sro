/*
* A scaling box. Here is an example of how to build a parameteric graph
* with nested nodes.
* 
* Author: Jeffrey Guenther
* September 27, 2012
*
* Lesson_2.sro
*
* Note: This is the syntax for a multi-line comment.
*/

// Define your nodes
node Point begin
    port x Value
    port y Value
    eval update Point ( x[0], y[0] )
    port point Value ( update[0] )  // Shiro is case-sensitive, so "point" and "Point" are different names.
end

// Define a line node
node Line begin
    port p1 Value
    port p2 Value
    eval update Line ( p1[0], p2[0] )
    port length Value ( update["length"] )
    port line Value ( update[0] )
end

// QuadStats node with two evaluated ports
node QuadStats[update] begin
    port p1 Value
    port p2 Value
    port p3 Value
    port p4 Value
    eval areaFromPoints areaPoints(p1[0], p2[0], p3[0], p[4])

    port length1 Value
    port length2 Value
    eval update Multiply(length1[0], length2[0])
    
    // area depends on the two eval ports.
    // The active eval port will determine the value of area.
    port area Value (update["value"] | areaFromPoints["value"])

    node Perimeter begin
        port length1 Value(Area.length1[0])
        port length2 Value(Area.length2[0])
        eval update Sum(2 * length1[0], 2 * length2[0])
        port perimeter Value(update[0])
    end

    port perimeter Value(Perimeter.perimeter[0])
end

// Graph
graph constrainedBox begin
    Point -> P1 -> P2 -> P3 -> P4
    Line -> L1 -> L2 -> L3 -> L4
    QuadStats -> Q[update]

    // Connect nodes in the graph by connecting ports via a port's expression
    P1.x(0)
    P1.y(0)
    
    P2.x( 0 )
    P2.y( 10)
        
    P3.x( 2.5 * P2.y[0])
    P3.y( P2.y[0])
    
    P4.x(2.5 * P2.y[0])
    P4.y(P1.y[0])
    
    L1.p1(P1.point[0])
    L1.p2(P2.point[0])
    
    L2.p1(P1.point[0])
    L2.p2(P4.point[0])
    
    L3.p1(P4.point[0])
    L3.p2(P3.point[0])
    
    L4.p1(P2.point[0])
    L4.p2(P3.point[0])
    
    Q.length1(L1.length[0])
    Q.length2(L2.length[0])
end

// declaring a state
state Lesson2 begin
    Graph constrainedBox        // indicate which graph to use in the state
end