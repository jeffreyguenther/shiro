/**
 * This build script is based on https://github.com/ae6rt/gradle-antlr4-template
 * 
 * */
description = "shiro - a data flow language for alternatives"
apply plugin: 'maven'
apply plugin: 'signing'

apply plugin: 'java'
apply plugin: 'application'
apply from: 'gradle/javafx.plugin'

version = '0.5.1-SNAPSHOT'
group = 'org.shirolang'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
    }
}

ext.versions = [
    junit: "4.11"
]

ext.antlr = [
    grammarpackage: "org.shirolang.interpreter",
    antlrSource: 'src/main/java/org/shirolang',
    destinationDir: "src/generated/java"
]

sourceSets{
    generated
    main{
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }
    test{
        compileClasspath += generated.output
        runtimeClasspath += generated.output
    }
}
project.run.classpath += sourceSets.generated.output

repositories {
    mavenCentral()
}

configurations {
    antlr4
}

sourceSets{
    main{
        java{
            srcDirs 'src/main/java', 'src/generated/java'
        }
    }
}

dependencies {
    compile group: "org.antlr", name: "antlr4-runtime", version: "4.5"
    generatedCompile group: "org.antlr", name: "antlr4-runtime", version: "4.5"
    compile 'org.antlr:ST4:4.0.8'
    compile group: "org.antlr", name: "antlr4", version: "4.5"

    compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.6.10'
    compile 'com.google.guava:guava:18.0'
    compile 'net.sf.opencsv:opencsv:2.3'
//    compile 'net.sf.jung:jung-api:2.0.1'
//    compile 'net.sf.jung:jung-graph-impl:2.0.1'
//    compile 'net.sf.jung:jung-algorithms:2.0.1'
//    compile 'net.sf.jung:jung-io:2.0.1'
//    compile 'net.sf.jung:jung-visualization:2.0.1'
    
    
    testCompile group: "junit", name: "junit", version: versions.junit
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties = System.getProperties()
    testLogging.showStandardStreams = true
}

task antlrOutputDir << {
    mkdir(antlr.destinationDir)
}

task generateGrammarSource(dependsOn: antlrOutputDir, type: JavaExec) {
    description = 'Generates Java sources from ANTLR4 grammars.'

    inputs.dir file(antlr.antlrSource)
    outputs.dir file(antlr.destinationDir)
    
    def grammars = fileTree(antlr.antlrSource).include('**/*.g4')

    main = 'org.antlr.v4.Tool'
    classpath = sourceSets.main.compileClasspath
    def pkg = antlr.grammarpackage.replaceAll("\\.", "/")
    args = ["-o", "${antlr.destinationDir}/${pkg}"/*, "-atn"*/, "-listener", "-package", antlr.grammarpackage, grammars.files].flatten()
}

compileJava {
    dependsOn generateGrammarSource
    classpath += sourceSets.generated.output
}

clean {
    delete antlr.destinationDir
}

// maven deployment code based on https://github.com/TomasMikula/RichTextFX
javadoc {
    // ignore missing Javadoc comments or tags
    options.addStringOption('Xdoclint:all,-missing', '-quiet')

    // support for JavaFX properties
    options.addBooleanOption('javafx', true);

    // resolve links to Java and JavaFX Javadocs
    options.links = [
            'http://docs.oracle.com/javase/8/docs/api/',
            'http://docs.oracle.com/javase/8/javafx/api/'
    ]
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

signArchives.onlyIf {
    project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')
}

def doUploadArchives = project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')

if(doUploadArchives) {
    uploadArchives {
        repositories.mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Shiro'
                packaging 'jar'
                description 'Experimental dataflow programming language'
                url 'http://shirolang.org'

                scm {
                    url 'scm:git@github.com:jrguenther/shiro.git'
                    connection 'scm:git@github.com:jrguenther/shiro.git'
                    developerConnection 'scm:git@github.com:jrguenther/shiro.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name 'Jeffrey Guenther'
                    }
                }
            }
        }
    }
}

uploadArchives.onlyIf { doUploadArchives }

task fatJar(type: Jar, dependsOn: classes) {
    appendix = 'fat'
    from sourceSets.main.output
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}
assemble.dependsOn fatJar
// end deployment code

mainClassName = 'org.shirolang.playground.ShiroPlayground'

javafx{
    appID 'Shiro Playground'
    appName 'Shiro Playground'
    mainClass mainClassName
}
