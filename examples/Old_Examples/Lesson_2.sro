/*
* A scaling box. Here is an example of how to build a parameteric graph
* with nested nodes.
* 
* Author: Jeffrey Guenther
* September 27, 2012
*
* Lesson_2.sro
*
* Note: This is the syntax for a multi-line comment.
*/

// Define your nodes
node Point begin
    input x Value
    input y Value
    eval update Point ( x[0], y[0] )
    output point Value ( update[0] )  // Shiro is case-sensitive, so "point" and "Point" are different names.
end

// Define a line node
node Line begin
    input p1 Value
    input p2 Value
    eval update Line ( p1[0], p2[0] )
    output length Value ( update["length"] )
    output line Value ( update[0] )
end

// QuadStats node with two evaluated ports
node QuadStats[update] begin
    input p1 Value
    input p2 Value
    input p3 Value
    input p4 Value
    option eval areaFromPoints areaPoints(p1[0], p2[0], p3[0], p[4])

    input length1 Value
    input length2 Value
    option eval update Multiply(length1[0], length2[0])
    
    // area depends on the two eval ports.
    // The active eval port will determine the value of area.
    output area Value (active["value"])

    node Perimeter begin
        input length1 Value(Area.length1[0])
        input length2 Value(Area.length2[0])
        eval update Sum(2 * length1[0], 2 * length2[0])
        output perimeter Value(update[0])
    end

    output perimeter Value(Perimeter.perimeter[0])
end

// Graph
graph constrainedBox begin
    Point -> P1 -> P2 -> P3 -> P4
    Line -> L1 -> L2 -> L3 -> L4
    QuadStats -> Q[update]

    // Connect nodes in the graph by connecting ports via a port's expression
    P1.x(0)
    P1.y(0)
    
    P2.x( 0 )
    P2.y( 10)
        
    P3.x( 2.5 * P2.y[0])
    P3.y( P2.y[0])
    
    P4.x(2.5 * P2.y[0])
    P4.y(P1.y[0])
    
    L1.p1(P1.point[0])
    L1.p2(P2.point[0])
    
    L2.p1(P1.point[0])
    L2.p2(P4.point[0])
    
    L3.p1(P4.point[0])
    L3.p2(P3.point[0])
    
    L4.p1(P2.point[0])
    L4.p2(P3.point[0])
    
    Q.length1(L1.length[0])
    Q.length2(L2.length[0])
end

// declaring a state
state Lesson2 begin
    Graph constrainedBox        // indicate which graph to use in the state
end